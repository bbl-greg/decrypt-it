plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'com.diffplug.spotless' version '6.4.2'
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

group 'dev.gregdrake'
version '1.0-SNAPSHOT'

project.ext {
    dropwizardVersion = '2.1.0-beta.8'
    dropwizardGuicyVersion = '5.5.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    implementation 'ru.vyarus:dropwizard-guicey:' + dropwizardGuicyVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

jar {
    manifest {
        attributes 'Main-Class': 'dev.gregdrake.DecryptItApplication'
    }
}

spotless {
    // optional: limit format enforcement to just the files changed by this feature branch
    ratchetFrom 'origin/main'

    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '*.md', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
        endWithNewline()
    }
    java {
        // don't need to set target, it is inferred from java

        // apply a specific flavor of google-java-format
        googleJavaFormat('1.15.0').aosp().reflowLongStrings()
        // make sure every file has the following copyright header.
        // optionally, Spotless can set copyright years by digging
        // through git history (see "license" section below)
        licenseHeader '/* (C)$YEAR */'
    }
}
